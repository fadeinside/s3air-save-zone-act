//@ Function taken from sonic3air_dev/scripts/standalone/rendering/renderhooks.lemon
function bool Standalone.onWriteToSpriteTable(s16 px, s16 py, u16 renderQueue)
{
	// Data select menu, main game (just one moment, while timer won't be updated), DAGE and Achievements Plus custom menu
	if (global.game_mode == 0x4c || (global.game_mode == 0x0c && !hud.dirty.timer) || global.game_mode == 0x58 || global.game_mode == 0x9a)
	{
		// Character portait
		// - We render the act label in the character portait object
		if (objA0.update_address == 0x00d42c)
		{
			s16 posY
			u8 getMenuStyle = 0x01
			
		#if GAMEAPP >= 0x22051500
			if (Mods.isModActive("Data Select Character Sprites - MAIN"))
				getMenuStyle = System.getGlobalVariableValueByName("MenuStyle.dataSelect")
		#endif
			
			if (Mods.isModActive("Mania Style Data Select"))
				posY = (getMenuStyle == 0x00) ? 64 : 54
			else
				posY = (getMenuStyle == 0x00) ? 48 : 40
			
			u32 selectedSlotData = 0xffffe6ac + (0x0a * (dataselect.slot_selected - 0x01))
			
			bool nonCompletedSlot = u8[selectedSlotData + 0x00] == 0x00 && u8[A0 + 0x9a] != 0x00 && u8[A0 + 0x08] != 0x01
			bool completedSlot = u8[selectedSlotData + 0x00] != 0x00 && u8[A0 + 0x9a] != 0x00 && u8[A0 + 0x08] != 0x01
			
			if (completedSlot || nonCompletedSlot)
			{
				// Define the number of the act
				string key_act
				if (Mods.isModActive("Mania Style Data Select"))
					key_act = stringformat("savelabel_mania_act_%d", u8[A0 + 0x9a])
				else if (Mods.isModActive("Sonic 3 Complete Font") || Mods.isModActive("Sonic 3 Complete Sound Test"))
					key_act = stringformat("savelabel_complete_act_%d", u8[A0 + 0x9a])
				else
					key_act = stringformat("savelabel_act_%d", u8[A0 + 0x9a])
				
				// Define the boss battle sprite
				string key_boss
				if (Mods.isModActive("Mania Style Data Select"))
					key_boss = "savelabel_mania_act_boss"
				else if (Mods.isModActive("Sonic 3 Complete Font") || Mods.isModActive("Sonic 3 Complete Sound Test"))
					key_boss = "savelabel_complete_act_boss"
				else
					key_boss = "savelabel_act_boss"
				
				// Define whether to use an Boss label or Act label for a slot
				bool bossBattle = (u8[A0 + 0x9a] == 0x0a) ? true : false
				string key_label = bossBattle ? key_boss : key_act
				
				// Check if the sprite exists
				if (Renderer.hasCustomSprite(key_label))
				{
					Renderer.drawCustomSprite(key_label, px, py - posY, 0x00, SPRITE_FLAG_PRIO, (posY > 48) ? 0xffff : renderQueue + 0x01)
				}
			}
		}
	}
	
	// Return the base function
	return base.Standalone.onWriteToSpriteTable(px, py, renderQueue)
}