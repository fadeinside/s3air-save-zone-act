function bool Standalone.onWriteToSpriteTable(s16 px, s16 py, u16 renderQueue)
{
	if ((global.game_mode & 0x7f) == 0x4c || (global.game_mode & 0x7f) == 0x0c)
	{
		// Zone act label
		if (objA0.update_address == 0x00d42c)
		{
			u32 selectedSlotData = 0xffffe6ac + (0x0a * (dataselect.slot_selected - 0x01))
			
			bool nonCompletedSlot = u8[selectedSlotData + 0x00] == 0x00 && u8[A0 + 0x75] != 0x00 && u8[A0 + 0x08] != 0x01
			bool completedSlot = u8[selectedSlotData + 0x00] != 0x00 && u8[A0 + 0x75] != 0x00 && u8[A0 + 0x08] != 0x01
			
			if (completedSlot || nonCompletedSlot)
			{
				// "Sonic 3 Complete Font" by Retro, check https://gamebanana.com/mods/334378
				// "Sonic 3 Complete Sound Test" by mrgrassman14, check https://gamebanana.com/mods/361615
				// "Mania Style Data Select" by Agnoristos, check https://gamebanana.com/mods/323556
				
				// Define the number of the act
				u64 key_act
				if (Mods.isModActive("Mania Style Data Select"))
					key_act = stringformat("dataSel_saveLabel_Mania_ACT_%d", u8[A0 + 0x75])
				else if (Mods.isModActive("Sonic 3 Complete Font") || Mods.isModActive("Sonic 3 Complete Sound Test"))
					key_act = stringformat("dataSel_saveLabel_Complete_ACT_%d", u8[A0 + 0x75])
				else
					key_act = stringformat("dataSel_saveLabel_ACT_%d", u8[A0 + 0x75])
				
				// Define the boss battle sprite
				u64 key_boss
				if (Mods.isModActive("Mania Style Data Select"))
					key_boss = "dataSel_saveLabel_Mania_ACT_BOSS"
				else if (Mods.isModActive("Sonic 3 Complete Font") || Mods.isModActive("Sonic 3 Complete Sound Test"))
					key_boss = "dataSel_saveLabel_Complete_ACT_BOSS"
				else
					key_boss = "dataSel_saveLabel_ACT_BOSS"
				
				// Define whether to use an Boss label or Act label for a slot
				bool bossBattle = (u8[A0 + 0x75] == 0x0a) ? true : false
				u64 key_label = bossBattle ? key_boss : key_act
				
				// Check if the sprite exists
				if (Renderer.hasCustomSprite(key_label))
				{
					if (Mods.isModActive("Mania Style Data Select"))
						Renderer.drawCustomSprite(key_label, px, py - (MenuStyle.dataSelect == 0x00 ? 64 : 56), 0x00, SPRITE_FLAG_PRIO, 0xffff)
					else
						Renderer.drawCustomSprite(key_label, px, py - (MenuStyle.dataSelect == 0x00 ? 48 : 40), 0x00, SPRITE_FLAG_PRIO, renderQueue + 0x01)
				}
			}
		}
	}
	
	// Return the base function
	return base.Standalone.onWriteToSpriteTable(px, py, renderQueue)
}